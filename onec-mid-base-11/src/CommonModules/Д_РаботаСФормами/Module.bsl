#Область ПрограммныйИнтерфейс
//Дополним форму документа
//Параметры:
//Форма - ФормаКлиентскогоПриложения
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьГруппаСкидка(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
    ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;	
	
КонецПроцедуры  

//Дополним форму документа
//Параметры:
//Форма - ФормаКлиентскогоПриложения
Процедура ДобавитьГруппаСкидка(Форма) Экспорт
	
	ГруппаФормы = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);  
	ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;   
	
	ДобавитьПолеВГруппаСкидка(Форма);
	ДобавитьКомандуВГруппаСкидка(Форма);
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";	

КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";	

КонецПроцедуры

Процедура ДобавитьПолеВГруппаСкидка(Форма) Экспорт

	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_СогласованнаяСкидка"; 
	//@skip-check module-attachable-event-handler-name
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");

КонецПроцедуры

Процедура ДобавитьКомандуВГруппаСкидка(Форма) Экспорт
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицы");
	Команда.Заголовок = "Пересчитать таблицы";
	Команда.Действие = "ПересчитатьТаблицы"; 
	Команда.Картинка = БиблиотекаКартинок.Перечитать; 
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;

	КнопкаФормы = Форма.Элементы.Добавить("ПересчитатьТаблицы", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка); 
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицы";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
		
КонецПроцедуры

#КонецОбласти
